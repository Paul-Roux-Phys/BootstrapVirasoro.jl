"""
Block

Abstract supertype for all conformal block types.

# Hierarchy

```text
Block  (Abstract Type)
‚îú‚îÄ ChiralBlock
‚îú‚îÄ NonChiralBlock  (Abstract Type)
‚îÇ  ‚îú‚îÄ Factorised block
‚îÇ  ‚îî‚îÄ LogBlock
‚îî‚îÄ LinearCombinationBlock
```
"""
abstract type Block{T} end # general conformal block. Can be interchiral, non-chiral or chiral

"""
Type to represent the series expansion of a chiral block.
Aliased to CBlock.
"""
struct ChiralBlock{T} <: Block{T}
    corr::CCo{T}
    chan_dim::CD{T}
    coeffs::Vector{T}
    coeffs_der::Vector{T}
    missing_terms::Vector{T}
    chan::Symbol
    Œîmax::Int
end
const CBlock = ChiralBlock

"""
Abstract supertype to represent the series expansion of a non chiral block.
Aliased to NCBlock.
"""
abstract type NonChiralBlock{T} <: Block{T} end
const NCBlock = NonChiralBlock

struct FactorizedBlock{T} <: NonChiralBlock{T}
    chan_field::Field{T}
    corr::Corr
    cblocks::LR{CBlock{T}}
    Œîmax::Int
    chan::Symbol
end

struct LogBlock{T} <: NonChiralBlock{T}
    cblocks::LR{CBlock{T}} # blocks for V_(r, s) x V_(r, -s)
    cblocks_op::LR{CBlock{T}} # blocks for V_(r, -s) x V_(r, s)
    cblocks_der::Union{LR{CBlock{T}},Nothing}
    corr::Corr
    chan_field::Field{T}
    R::T
    Rbar::T
    ell::T
    Œîmax::Int
    nbzeros::Int
    chan::Symbol
end

abstract type LinearCombinationBlock{T} <: Block{T} end
const LCBlock = LinearCombinationBlock

struct GenericLCBlock{T} <: LCBlock{T}
    chan_field::Field{T}
    blocks::Vector{Block{T}}
    coeffs::Vector{T}
    chan::Symbol
end

#=============================================================================
Chiral Blocks
=================================++++========================================#
function series_H(d::CD{T}, Œîmax, CNmn) where {T}
    P = d.P
    P2 = P^2
    isKac = d.isKac
    r, s = d.r, d.s

    coeffs = Matrix{T}(undef, Œîmax + 2, Œîmax + 2)
    all_mns = union([CNmn.keys[N] for N = 1:Œîmax]...)
    for (m, n) in all_mns
        if isKac && m == r && n == s
            coeffs[m, n] = -inv(4CNmn.Œ¥s[r, s])
        else
            coeffs[m, n] = inv(P2 - CNmn.Œ¥s[m, n])
        end
    end

    H = zeros(T, Œîmax + 1)
    H[1] = one(T)
    for N = 1:Œîmax
        for (m, n) in CNmn.keys[N]
            (N > Œîmax || m > Œîmax || n > Œîmax) && continue
            H[N+1] += CNmn[N, m, n] * coeffs[m, n]
        end
    end

    return H
end

function series_H_der(d::CD{T}, Œîmax, CNmn) where {T}
    P = d.P
    P2 = P^2

    H = zeros(T, Œîmax + 1)
    coeffs = Dict(
        (m, n) => -2P / (P2 - CNmn.Œ¥s[m, n])^2 for
        (m, n) in union([CNmn.keys[N] for N = 1:Œîmax]...)
    )

    for N = 1:Œîmax
        for (m, n) in CNmn.keys[N]
            (N > Œîmax || m > Œîmax || n > Œîmax) && continue
            H[N+1] += CNmn[N, m, n] * coeffs[(m, n)]
        end
    end

    return H
end

"""
        ChiralBlock(::ChiralCorrelation4, ::Symbol, ::CD, Œîmax=co.Œîmax::Int, der=false) # 4pt ùïä¬≤ chiral block
        ChiralBlock(::ChiralCorrelation1, ::CD, Œîmax=co.Œîmax::Int, der=false)           # 1pt ùïã¬≤ chiral block

Compute the series coefficients of a chiral block associated to the chiral correlation `co`, in the channel `chan`.
If `V` is a degenerate field, compute the ``P``-regularisation of the block instead.
If `der=true`, also compute the coefficients of the series expansion of the derivative of the block.

Aliased to CBlock.

# Arguments

- A chiral correlation object
- A channel for the block. Only required for four-point blocks.
- A channel `ConformalDimension` 
- `Œîmax`: integer up to which the series is evaluated.
- `der`: whether to compute the coefficients of the block's derivative as well.
"""
function ChiralBlock() end

function ChiralBlock(co::CCo{T}, chan::Symbol, d::CD, Œîmax = missing, der = false) where {T}
    Œîmax === missing && (Œîmax = co.Œîmax)
    CNmn = getCNmn(co, chan)
    coeffs = series_H(d, Œîmax, CNmn)
    coeffs_der = Vector{T}()
    der && (coeffs_der = series_H_der(d, Œîmax, CNmn))
    if !d.degenerate
        missing_terms = Vector{T}()
    else
        r, s = d.r, d.s
        missing_terms = [
            (N > 0 && (r, s) in CNmn.keys[N]) ? CNmn[N, r, s] : zero(T) for N = 0:Œîmax
        ]
    end

    CBlock{T}(co, d, coeffs, coeffs_der, missing_terms, chan, Œîmax)
end

function ChiralBlock(co::ChiralCorrelation1, d::CD = CD(), Œîmax=missing, der=false)
    CBlock(co, :œÑ, d, Œîmax, der)
end

getRmn(b::CBlock) = getRmn(b.corr, b.chan)
getRmnreg(b::CBlock) = getRmnreg(b.corr, b.chan)
getCNmn(b::CBlock) = getCNmn(b.corr, b.chan)
getc(b::CBlock) = b.corr.c


function Base.show(io::IO, ::MIME"text/plain", b::CBlock)
    println(io, "Chiral block for the correlation")
    println(io, b.corr)
    println(io, "Channel: $(b.chan), $(b.chan_dim)")
end

function Base.show(io::IO, b::CBlock)
    println(io, "F^($(b.chan))($(b.chan_dim))")
end

#================================================================================
Non Chiral blocks
================================================================================#
function FactorizedBlock(co::NCCo{T}, chan, V, Œîmax) where {T}
    bl = CBlock(co[:left], chan, V[:left], Œîmax)
    br = CBlock(co[:right], chan, V[:right], Œîmax)
    FactorizedBlock{T}(V, co, LR(bl, br), Œîmax, chan)
end

# function islogarithmic(V::Field)
    # r, s = V.r, V.s
    # !V.diagonal && r * s != 0 && r % 1 == s % 1 == 0 && r > 0 && s > 0
# end

islogarithmic(V::Field) = !V.diagonal && V[:left].degenerate
islogarithmic(b::NCBlock) = islogarithmic(b.chan_field)

function isaccidentallynonlogarithmic(co::NCCo, chan, V)
    !islogarithmic(V) && return false
    return Rmn_zero_order(V.r, V.s, getfields(co[:left], chan)) > 0
end

isaccidentallynonlogarithmic(b::NCBlock) =
    isaccidentallynonlogarithmic(b.corr, b.chan, b.chan_field)

function LogBlock(co::NCCo{T}, chan, V, Œîmax) where {T}
    V_op = swap_lr(V)
    VV = V.s > 0 ? (V, V_op) : (V_op, V) # (V_(r, s > 0), V_(r, -s))
    r, s = VV[1].r, VV[1].s
    left, left_op, right, right_op = Tuple(
        CBlock(co[lr], chan, v.dims[lr], Œîmax, false) for lr in (:left, :right)
        for v in VV
    )
    R, Rbar, l = zero(T), zero(T), zero(T)
    cl, cr = co[:left], co[:right]
    if isaccidentallynonlogarithmic(co, chan, V)
        chiral_blocks_der = nothing
        if V.s > 0
            R = getRmnreg(cl, chan)[r, s]
            Rbar = getRmnreg(cr, chan)[r, s]
        end
        nbzeros = Rmn_zero_order(r, s, left.corr.fields)
        l = zero(T)
    else
        leftder = CBlock(cl, chan, VV[2][:left], Œîmax, true)
        rightder = CBlock(cr, chan, VV[1].dims.right, Œîmax, true)
        chiral_blocks_der = LR(leftder, rightder)
        if V.s > 0
            R = getRmn(cl, chan)[r, s]
            Rbar = getRmn(cr, chan)[r, s]
            l = ell(getfields(co, chan), r, VV[1].s)
        end
        nbzeros = 0
    end
    LogBlock{T}(
        LR(left, right),
        LR(left_op, right_op),
        chiral_blocks_der,
        co,
        V,
        R,
        Rbar,
        l,
        Œîmax,
        nbzeros,
        chan,
    )
end

reflect(b::NonChiralBlock) =
    FactorizedBlock(b.corr, b.channel, reflect(b.chan_field), b.Œîmax)

"""
        NCBlock(::NonChiralCorrelation, ::Symbol, ::Field, Œîmax=co.Œîmax::Int) # 4pt ùïä¬≤ chiral block
        NCBlock(::NonChiralCorrelation, ::Field, Œîmax=co.Œîmax::Int)           # 1pt ùïã¬≤ chiral block

Compute the series coefficients of the left and right blocks associated to the non chiral correlation `co`, in the channel `chan`.
If `V` is a degenerate field, compute the ``P``-regularisation of the block instead.
If `V` is a non-diagonal field with left dimension degenerate, 
If `der=true`, also compute the coefficients of the series expansion of the derivative of the block.

# Arguments

- A `NonChiralCorrelation` object
- A channel for the block. Only required for four-point blocks.
- A channel `Field` 
- `Œîmax`: integer up to which the series is evaluated. Defaults to the correlation's `Œîmax`.
"""
function NonChiralBlock() end

function NonChiralBlock(co::NCCo, chan, V, Œîmax=missing)
    Œîmax === missing && (Œîmax = co.Œîmax)
    if islogarithmic(V)
        LogBlock(co, chan, V, Œîmax)
    else
        FactorizedBlock(co, chan, V, Œîmax)
    end
end
NonChiralBlock(co::NonChiralCorrelation1, V, Œîmax=missing) = NCBlock(co, :œÑ, V, Œîmax)

Base.getindex(b::NCBlock, s::Symbol) = b.cblocks[s]

function ell(V::NTuple{4,Field}, r, s)
    Œ≤ = V[1].c.Œ≤
    Œ≤sq = Œ≤^2
    res = 4 * (œÄ / tan(s * (œÄ / Œ≤sq)))
    res +=
        4 * sum(digamma_reg(-r + j / Œ≤sq) + digamma_reg(r + j / Œ≤sq) for j = (1-s):s)
    res -= sum(
        digamma_reg(
            1 // 2 +
            (lr == :left ? -1 : 1) *
            (Prs(r, j, Œ≤) + pm1 * V[a].dims[lr].P + pm2 * V[b].dims[lr].P) / Œ≤,
        ) for pm1 in (-1, 1) for pm2 in (-1, 1) for j = (1-s):2:(s-1) for
        (a, b) in ((1, 2), (3, 4)) for lr in (:left, :right)
    )
    res / Œ≤
end

function ell(V::NTuple{1,Field}, r, s)
    Œ≤ = V[1].c.Œ≤
    Œ≤sq = Œ≤^2
    res = 4 * (œÄ / tan(s * (œÄ / Œ≤^2)))

    res +=
        4 * sum(digamma_reg(-r + j / Œ≤sq) + digamma_reg(r + j / Œ≤sq) for j = (1-s):s)
    res -=
        2 * sum(
            digamma_reg(
                1 // 2 +
                pm1 / Œ≤^2 / 2 +
                (pm2 * V[1].dims[lr].P + 2 * (lr == :left ? -1 : 1) * Prs(r, j, Œ≤)) /
                Œ≤,
            ) for j = (1-s):2:(s-1) for pm1 in (-1, 1) for pm2 in (-1, 1) for
            lr in (:left, :right)
        )
    res / Œ≤
end

function Base.show(io::IO, b::NCBlock)
    print(io, "G^($(b.chan))($(b.chan_field))")
end

function Base.show(io::IO, ::MIME"text/plain", b::NCBlock)
    print(io, "Non chiral")
    if typeof(b) <: LogBlock
        print(io, " logarithmic ")
    else
        print(io, " factorised ")
    end
    println(io, "block for the correlation")
    println(io, b.corr)
    println(io, "channel: $(b.chan), $(b.chan_field)")
end

#====================================================================================
Linear Combinations of blocks
====================================================================================#
function LinearCombinationBlock(bs::Vector{<:Block{T}}, coeffs) where {T}
    GenericLCBlock{T}(bs[1].chan_field, bs, coeffs, bs[1].chan)
end

function Base.:+(b1::LCBlock, b2::LCBlock)
    LCBlock(vcat(b1.blocks, b2.blocks), vcat(b1.coeffs, b2.coeffs))
end

function Base.:+(b1::LCBlock, b2::Block)
    LCBlock(vcat(b1.blocks, b2), vcat(b1.coeffs, 1))
end

function Base.:+(b1::Block, b2::LCBlock)
    LCBlock(vcat(b1, b2.blocks), vcat(1, b2.coeffs))
end

function Base.:+(b1::Block, b2::Block)
    LCBlock(vcat(b1, b2), vcat(1, 1))
end

function Base.:-(b1::Block, b2::Block)
    LCBlock(vcat(b1, b2), vcat(1, -1))
end

function Base.:*(a::Number, b::Block)
    LCBlock([b], [a])
end

function Base.show(io::IO, b::LCBlock)
    coeffs = [
        if c ‚âà round(Int, real(c))
            round(Int, c)
        else
            c
        end for c in b.coeffs
    ]
    print(io, "($(coeffs[1])) * $(b.blocks[1])")
    for (i, bl) in enumerate(b.blocks[2:end])
        print(io, " + ($(coeffs[i+1])) * $bl")
    end
end
